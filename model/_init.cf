entity HBaseBasic extends ip::services::Server:

end 

entity HBaseMaster:
    string port = "60000"
end

entity HBaseRegion:
    string port = "60020"
end

entity HadoopHDFS:
    string port = "9000"
    string directory = "hbase"
    string nameDir = "/var/lib/hadoop-hdfs/cache/hdfs/dfs/name"
end


entity HadoopDataHDFS:
    string port = "9000"
    string directory = "hbase"
    string dataDir = "/var/lib/hadoop-hdfs/cache/hdfs/dfs/data"
end

entity Zookeeper:
    string port = "2181"
    string directory = "/var/lib/zookeeper"
    string number = "1"
end

entity HBaseCluster:

end


HBaseCluster cluster [1] -- [1:] HBaseMaster masters
HBaseCluster cluster [1] -- [1:] HBaseRegion regions
HBaseCluster cluster [1] -- [1] HadoopHDFS hdfs
HBaseCluster cluster [1] -- [1:] Zookeeper zookeepers

HadoopHDFS hdfs [1] -- [0:] HadoopDataHDFS dataNodes

HBaseBasic host [1] -- [0:1] HBaseMaster master
HBaseBasic host [1] -- [0:1] HBaseRegion regions
HBaseBasic host [1] -- [0:1] HadoopHDFS hdfs
HBaseBasic host [1] -- [0:1] HadoopDataHDFS dataNodes
HBaseBasic host [1] -- [0:1] Zookeeper zookeepers


implement HBaseMaster using masterImpl, fileIncrease
implement HBaseRegion using regionImpl, fileIncrease
implement Zookeeper using zookeeperImpl
implement HadoopHDFS using hdfsImplMaster
implement HadoopDataHDFS using hdfsDataNodeImpl
implement HBaseCluster using std::none
implement HBaseBasic using hbaseBasic

implementation hbaseBasic:
    java = std::Package(host = host, name = "java", state = "installed")
    r = yum::Repository(host = host, name = "cloudera-cdh4", baseurl = "http://archive.cloudera.com/cdh4/redhat/6/x86_64/cdh/4.5.0/")
    r2 = std::File(host = host, path = "/etc/yum.repos.d/fedora.repo", 
                              content = file("hbase/fedora.repo"), 
                             reload = true,
                             owner = "root", group = "root", mode = 644)
    r3 = std::File(host = host, path = "/etc/yum.repos.d/fedora-updates.repo", 
                              content = file("hbase/fedora-updates.repo"), 
                             reload = true,
                             owner = "root", group = "root", mode = 644)
    
    pkg = std::Package(host = host, name = "hbase", state = "installed", requires = r)

end

implementation masterImpl:
    pkg = std::Package(host = host.host, name = "hbase-master", state = "installed")
    
    config = std::ConfigFile(host = host.host, path = "/etc/hbase/conf/hbase-site.xml", content = template("hbase/hbase-site-master.conf.tmpl"), 
                             requires = pkg, reload = true)
    
    svc = std::Service(host = host.host, name = "hbase-master", state = "running", onboot = true)
    svc.requires = [pkg, config]
    svc.requires = std::Service[host = host.host, name = "hbase-file-increase"]
    svc.requires = std::Package[host = host.host, name = "java"]
end

implementation regionImpl:
    pkg = std::Package(host = host.host, name = "hbase-regionserver", state = "installed")
   
    config = std::ConfigFile(host = host.host, path = "/etc/hbase/conf/hbase-site.xml", content = template("hbase/hbase-site-region.conf.tmpl"), 
                             requires = pkg, reload = true)
        
    svc = std::Service(host = host.host, name = "hbase-regionserver", state = "running", onboot = true)
    svc.requires = [pkg, config]
    svc.requires = std::Service[host = host.host, name = "hbase-file-increase"]
    svc.requires = std::Package[host = host.host, name = "java"]
end

implementation zookeeperImpl:
    pkg = std::Package(host = host.host, name = "zookeeper-server", state = "installed")
    
    svcFile = std::File(host = host.host, path = "/etc/init.d/zookeeper-server", 
                              content = template("hbase/zookeeper-server.service.tmpl"), 
                             requires = pkg, reload = true,
                             owner = "root", group = "root", mode = 755)
    zookeeperdir = std::Directory(host = host.host, path = directory, owner = "root", group = "root", mode = 777, reload = true, 
    			requires = pkg)
    			
    myidFile = std::File(host = host.host, path = "{{directory}}/myid", 
                              content = template("hbase/zookeeper-myid.tmpl"), 
                             requires = zookeeperdir, reload = true,
                             owner = "root", group = "root", mode = 755)
                             
    svc = std::Service(host = host.host, name = "zookeeper-server", state = "running", onboot = true)
    svc.requires = [pkg, svcFile, myidFile]
    svc.requires = std::Package[host = host.host, name = "java"]
end

implementation hdfsImplMaster:
    pkg = std::Package(host = host.host, name = "hadoop-hdfs", state = "installed")
    pkgNameNode = std::Package(host = host.host, name = "hadoop-hdfs-namenode", state = "installed", requires = pkg)
        
    coresite = std::ConfigFile(host = host.host, path = "/etc/hadoop/conf/core-site.xml", 
                               content = template("hbase/core-site.xml.tmpl"), 
                               requires = pkg, reload = true)
    
    hbasehdfs = std::ConfigFile(host = host.host, path = "/etc/hadoop/conf/hdfs-site.xml", 
                               content = template("hbase/hdfs-site.xml.tmpl"), 
                               requires = pkg, reload = true)
    
    # Formatting the namenode 
    fileFormat = std::File(host = host.host, path = "/usr/bin/hbase-hdfs-format", content = template("hbase/hbase-hdfs-format.tmpl"), 
                             owner = "root", group = "root", mode = 755)
    
    svcFormatFile = std::File(host = host.host, path = "/lib/systemd/system/hbase-hdfs-format.service", 
                                content = file("hbase/hbase-hdfs-format.service"), owner = "root", 
                                group = "root", mode = 755)
    svcFormat = std::Service(host = host.host, name = "hbase-hdfs-format", state = "running", onboot = true,
                           requires = [fileFormat, svcFormatFile])
                           
                           
    slavesFile = std::File(host = host.host, path = "/etc/hadoop/conf/slaves", 
                                content = template("hbase/slaves.tmpl"), owner = "root", 
                                group = "root", mode = 755,
                                requires = pkgNameNode)
    # Starting the nodes
    svcNameNode = std::Service(host = host.host, name = "hadoop-hdfs-namenode", state = "running", onboot = true,
                           requires = [pkg, pkgNameNode, coresite, hbasehdfs, svcFormat, slavesFile])
    

    
    #Starting the init
#    fileInit = std::File(host = host.host, path = "/usr/bin/hbase-hdfs-init", content = template("hbase/hbase-hdfs-init.tmpl"), 
#                             owner = "root", group = "root", mode = 755)
    
#    svcInitFile = std::File(host = host.host, path = "/lib/systemd/system/hbase-hdfs-init.service", 
#                                content = file("hbase/hbase-hdfs-init.service"), owner = "root", 
#                                group = "root", mode = 755)
#    svcInit = std::Service(host = host.host, name = "hbase-hdfs-init", state = "running", onboot = true,
#                           requires = [fileInit, svcInitFile, svcNameNode])
end

implementation hdfsDataNodeImpl:
    pkgDataNode = std::Package(host = host.host, name = "hadoop-hdfs-datanode", state = "installed")
    
    coresite = std::ConfigFile(host = host.host, path = "/etc/hadoop/conf/core-site.xml", 
                               content = template("hbase/core-site-data.xml.tmpl"), 
                               requires = pkgDataNode, reload = true)
    
    hbasehdfs = std::ConfigFile(host = host.host, path = "/etc/hadoop/conf/hdfs-site.xml", 
                               content = template("hbase/hdfs-site-data.xml.tmpl"), 
                               requires = pkgDataNode, reload = true)
    
    
    # Starting the nodes 
    svcDataNode = std::Service(host = host.host, name = "hadoop-hdfs-datanode", state = "running", onboot = true,
                           requires = [pkgDataNode, coresite, hbasehdfs])

    
    #Starting the init
    fileInit = std::File(host = host.host, path = "/usr/bin/hbase-hdfs-init", content = template("hbase/hbase-hdfs-init.tmpl"), 
                             owner = "root", group = "root", mode = 755)
    
    svcInitFile = std::File(host = host.host, path = "/lib/systemd/system/hbase-hdfs-init.service", 
                                content = file("hbase/hbase-hdfs-init.service"), owner = "root", 
                                group = "root", mode = 755)
    svcInit = std::Service(host = host.host, name = "hbase-hdfs-init", state = "running", onboot = true,
                           requires = [fileInit, svcInitFile, svcDataNode])
end


implementation fileIncrease:
    fileIncrease = std::File(host = host.host, path = "/usr/bin/hbase-file-increase", content = file("hbase/hbase-file-increase"), 
                             owner = "root", group = "root", mode = 755)
    svcFileIncrease = std::File(host = host.host, path = "/lib/systemd/system/hbase-file-increase.service", 
                                content = file("hbase/hbase-file-increase.service"), owner = "root", group = "root", mode = 755)
    
    svcIncrease = std::Service(host = host.host, name = "hbase-file-increase", state = "running", onboot = true, requires = [fileIncrease, svcFileIncrease])
end
